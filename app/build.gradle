apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.tg.multitest.multiapkdemo"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //添加一个默认的渠道号
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }


    //添加签名配置
    signingConfigs{
        debug{}
        release{
            storeFile file("D:\\develop\\multiDemo.jks")
            storePassword "123456"
            keyAlias "xudaha"
            keyPassword "123456"
        }
    }

    buildTypes {
        release {

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //配置签名文件
            signingConfig signingConfigs.release

            // 自定义输出配置
            applicationVariants.all { variant ->
                variant.outputs.all {
                    // 输出apk名称为multiDemo_v1.0_xiaomi.apk
                    def fileName = "multiDemo_v${defaultConfig.versionName}_${variant.productFlavors[0].name}_${releaseTime()}.apk"
                    outputFileName = fileName
                }
            }

        }
    }

    //多渠道配置
    flavorDimensions "default"
    productFlavors{
        xiaomi{
            dimension "default"
            //mainfestPlaceholder = [UMENG_CHANNEL_VALUE: "xiaomi"] 采用更简洁的方式
        }

        wandoujia{
            dimension "default"
            //mainfestPlaceholder = [UMENG_CHANNEL_VALUE: "wandoujia"]
        }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]//name会根据上面的配置来取
    }

}

static def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+8"))
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    //添加友盟依赖
    implementation 'com.umeng.sdk:common:1.5.0'
    implementation 'com.umeng.sdk:analytics:7.5.0'
}
